// =================================
// Copyright (c) 2017 Seppo Laakko
// Distributed under the MIT license
// =================================

[hpp]#include <soul_u32/codedom/Literal.hpp>

namespace soul.code
{
    [cpp]using namespace soul::codedom;
    
    grammar LiteralGrammar
    {
        Literal: soul::codedom::Literal*
                        ::= IntegerLiteral{ value = IntegerLiteral; }
                        |   CharacterLiteral{ value = CharacterLiteral; }
                        |   FloatingLiteral{ value = FloatingLiteral; }
                        |   StringLiteral{ value = StringLiteral; }
                        |   BooleanLiteral{ value = BooleanLiteral; }
                        |   PointerLiteral{ value = PointerLiteral; }
                        ;
                
        IntegerLiteral: soul::codedom::Literal*
                        ::= token(DecimalLiteral:dl IntegerSuffix:is1?) { value = new Literal(std::u32string(matchBegin, matchEnd)); }
                        |   token(OctalLiteral:ol IntegerSuffix:is2?){ value = new Literal(std::u32string(matchBegin, matchEnd)); }
                        |   token(HexadecimalLiteral:hl IntegerSuffix:is3?){ value = new Literal(std::u32string(matchBegin, matchEnd)); }
                        ;
                        
        DecimalLiteral  ::= token([1-9] digit*)
                        ;
                        
        OctalLiteral    ::= token('0' OctalDigit*)
                        ;
                        
        OctalDigit      ::= [0-7]
                        ;
                        
        HexadecimalLiteral
                        ::= token(("0x" | "0X")(hexdigit+))
                        ;
                        
        IntegerSuffix   ::= token(UnsignedSuffix (LongLongSuffix | LongSuffix)?)
                        |   token(LongLongSuffix UnsignedSuffix?)
                        |   token(LongSuffix UnsignedSuffix?)
                        ;

        UnsignedSuffix  ::= 'u' | 'U'
                        ;
                        
        LongLongSuffix  ::= "ll" | "LL"
                        ;
                        
        LongSuffix      ::= 'l' | 'L'
                        ;
                        
        CharacterLiteral: soul::codedom::Literal*
                        ::= NarrowCharacterLiteral{ value = new Literal(std::u32string(matchBegin, matchEnd)); }
                        |   UniversalCharacterLiteral{ value = new Literal(std::u32string(matchBegin, matchEnd)); }
                        |   WideCharacterLiteral{ value = new Literal(std::u32string(matchBegin, matchEnd)); }
                        ;
                            
        NarrowCharacterLiteral
                        ::= token('\'' CCharSequence '\'')
                        ;
                        
        UniversalCharacterLiteral
                        ::= token('u' '\'' CCharSequence:cs1 '\'')
                        |   token('U' '\'' CCharSequence:cs2 '\'')
                        ;
                        
        WideCharacterLiteral
                        ::= token('L' '\'' CCharSequence '\'')
                        ;
                        
        CCharSequence: std::u32string
                        ::= token(CChar+){ value = std::u32string(matchBegin, matchEnd); }
                        ;
                        
        CChar           ::= [^'\\\n]
                        |   EscapeSequence
                        ;
                        
        EscapeSequence  ::= SimpleEscapeSequence
                        |   OctalEscapeSequence
                        |   HexadecimalEscapeSequence
                        ;
                        
        SimpleEscapeSequence
                        ::= "\\'" | "\\\"" | "\\?" | "\\\\" | "\\a" | "\\b" | "\\f" | "\\n" | "\\r" | "\\t" | "\\v"
                        ;
                        
        OctalEscapeSequence
                        ::= '\\' OctalDigit OctalDigit? OctalDigit?
                        ;
                        
        HexadecimalEscapeSequence
                        ::= "\\x" hexdigit+
                        ;
                        
        FloatingLiteral: soul::codedom::Literal*
                        ::= token(FractionalConstant ExponentPart? FloatingSuffix?){ value = new Literal(std::u32string(matchBegin, matchEnd)); }
                        |   token(DigitSequence ExponentPart FloatingSuffix?){ value = new Literal(std::u32string(matchBegin, matchEnd)); }
                        ;
                        
        FractionalConstant
                        ::= DigitSequence? '.' DigitSequence
                        |   DigitSequence '.'
                        ;
                        
        DigitSequence   ::= digit+
                        ;
        
        ExponentPart    ::= ('e' | 'E') Sign? DigitSequence
                        ;
                        
        Sign            ::= '+' | '-'
                        ;
                        
        FloatingSuffix  ::= [fFlL]
                        ;
                        
        StringLiteral: soul::codedom::Literal*
                        ::= token(EncodingPrefix? '"' SCharSequence? '"'){ value = new Literal(std::u32string(matchBegin, matchEnd)); }
                        ;
                        
        EncodingPrefix  ::= "u8" | 'u' | 'U' | 'L'
                        ;
                        
        SCharSequence   ::= (SChar+)
                        ;
        
        SChar           ::= [^"\\\n]
                        |   EscapeSequence
                        ;
                        
        BooleanLiteral: soul::codedom::Literal*
                        ::= keyword("true"){ value = new Literal(U"true"); }
                        |   keyword("false"){ value = new Literal(U"false"); }
                        ;
                        
        PointerLiteral: soul::codedom::Literal*
                        ::= keyword("nullptr"){ value = new soul::codedom::Literal(U"nullptr"); }
                        ;
    }
}
