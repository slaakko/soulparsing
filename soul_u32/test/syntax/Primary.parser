// =================================
// Copyright (c) 2017 Seppo Laakko
// Distributed under the MIT license
// =================================

[hpp]#include <soul_u32/parsing/Parser.hpp>
[hpp]#include <soul_u32/parsing/Scope.hpp>
[cpp]#include <soul_u32/code/Expression.hpp>
[cpp]#include <soul_u32/code/Statement.hpp>
[cpp]#include <soul_u32/syntax/Primitive.hpp>
[cpp]#include <soul_u32/syntax/Composite.hpp>
[cpp]#include <soul_u32/syntax/Element.hpp>
[cpp]#include <soul_u32/parsing/Nonterminal.hpp>
[cpp]#include <soul_u32/codedom/Statement.hpp>

namespace soul.syntax
{
    [cpp]using namespace soul::code;

    grammar PrimaryGrammar
    {
        using PrimitiveGrammar.Primitive;
        using ElementGrammar.Identifier;
        using CompositeGrammar.Alternative;
        using soul.code.ExpressionGrammar.ExpressionList;
        using soul.code.StatementGrammar.CompoundStatement;

        Primary(soul::parsing::Scope* enclosingScope): soul::parsing::Parser*
                    ::=
                    (   RuleCall{ value = RuleCall; }
                    |   Primitive{ value = Primitive; }
                    |   Grouping(enclosingScope){ value = Grouping; }
                    |   Token(enclosingScope){ value = Token; }
                    )
                    (Expectation(value){ value = Expectation; })?
                    (Action(value){ value = Action; })?
                    ;

        RuleCall: soul::parsing::Parser*
                    ::= ((token(Nonterminal:ntWithArgs '(') ExpressionList ')' | Nonterminal:nt) Alias?)
                    {
                        std::u32string ruleName;
                        if (!ntWithArgs.empty())
                        {
                            ruleName = ntWithArgs;
                        }
                        else
                        {
                            ruleName = nt;
                        }
                        std::u32string alias = Alias;
                        std::u32string name = !alias.empty() ? alias : ruleName;
                        NonterminalParser* parser(new NonterminalParser(name, ruleName));
                        parser->SetSpan(span);
                        if (!ExpressionList.empty())
                        {
                            parser->SetArguments(ExpressionList); 
                        }
                        value = parser;
                    }
                    ;

        Nonterminal: std::u32string 
                    ::= Identifier{ value = Identifier; }
                    ;

        Alias: std::u32string
                    ::= ':' Identifier!{ value = Identifier; }
                    ;

        Grouping(soul::parsing::Scope* enclosingScope): soul::parsing::Parser*
                    ::= '(' Alternative(enclosingScope)! ')'!{ value = Alternative; }
                    ;

        Token(soul::parsing::Scope* enclosingScope): soul::parsing::Parser*
                    ::= keyword("token") '('! Alternative(enclosingScope)! ')'!{ value = new TokenParser(Alternative); }
                    ;

        Expectation(soul::parsing::Parser* child): soul::parsing::Parser* 
                    ::= '!'{ value = new ExpectationParser(child); }
                    ;

        Action(soul::parsing::Parser* child): soul::parsing::Parser*
                    ::= (CompoundStatement:action ('/' CompoundStatement:failureAction)?)
                    {
                        value = new ActionParser(U"", action, failureAction, child);
                    }
                    ;
    }
}
